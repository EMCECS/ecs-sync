/*
 * Copyright 2013-2015 EMC Corporation. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License").
 * You may not use this file except in compliance with the License.
 * A copy of the License is located at
 *
 * http://www.apache.org/licenses/LICENSE-2.0.txt
 *
 * or in the "license" file accompanying this file. This file is distributed
 * on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either
 * express or implied. See the License for the specific language governing
 * permissions and limitations under the License.
 */
allprojects {
    group = 'com.emc.ecs'
    version = '2.1.2'
}

ext.mainClass = 'com.emc.ecs.sync.EcsSync'

defaultTasks ':distZip'

buildscript {
    repositories {
        jcenter { url "https://jcenter.bintray.com/" }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'distribution'
apply plugin: 'idea'
apply plugin: 'eclipse'

repositories {
    mavenCentral()
    mavenLocal()
}

project(':ecs-sync-rest') {
    apply plugin: 'java'
    repositories {
        mavenCentral()
    }
    dependencies {
        compile 'com.sun.jersey:jersey-client:1.19'
        testCompile 'junit:junit:4.11'
    }
}

dependencies {
    compile files('lib/fplibrary-3.3.718.jar'),
            "org.slf4j:slf4j-api:1.7.12",
            "org.slf4j:slf4j-log4j12:1.7.12",
            "log4j:log4j:1.2.17",
            "commons-cli:commons-cli:1.3.1",
            "com.google.code.gson:gson:2.0",
            "com.emc.ecs:atmos-client:2.2.2.1",
            "com.emc.ecs:object-client:2.2.1",
            "mysql:mysql-connector-java:5.1.37",
            "org.springframework:spring-jdbc:3.2.6.RELEASE",
            "org.springframework:spring-context:3.2.6.RELEASE",
            "commons-dbcp:commons-dbcp:1.4",
            "com.amazonaws:aws-java-sdk-s3:1.10.5.1",
            "net.java.truevfs:truevfs-profile-default:0.11.0",
            "com.sun.jersey:jersey-server:1.19",
            "org.xerial:sqlite-jdbc:3.8.11.1",
            project(':ecs-sync-rest')
    testCompile "junit:junit:4.11",
            "org.hsqldb:hsqldb:2.3.2"
}

compileJava {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
    if (project.hasProperty('java7Lib')) {
        options.fork = true
        options.bootClasspath = new File(java7Lib).listFiles(
                [accept:{d, f-> f ==~ /.*\.jar/ }] as FilenameFilter
        ).join(File.pathSeparator)
    }
}

javadoc {
    options.quiet()
}

shadowJar {
    classifier ''
    mergeServiceFiles()
    append('META-INF/spring.handlers')
    append('META-INF/spring.schemas')
    manifest {
        attributes 'Main-Class': mainClass,
                'Implementation-Version': project.version,
                'Class-Path': { configurations.runtime.collect { it.getName() }.join(' ') }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from "${docsDir}/javadoc"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

distributions {
    main {
        contents {
            from 'readme.txt'
            from 'license.txt'
            from shadowJar
            from { project(':ecs-sync-ctl').shadowJar }
            from 'script'
            into('sample') {
                from 'sample'
            }
            into('docker') {
                from 'docker'
            }
            into('doc') {
                from javadocJar
            }
            into('src') {
                from sourcesJar
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}
